plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id "org.quiltmc.quilt-mappings-on-loom" version "4.2.0"
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/${archives_base_name}.accesswidener")

	runs {
		client {
			client()
			setConfigName("Fabric Client")
			ideConfigGenerated(true)
			runDir("../run")
		}
	}
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven { url 'https://jitpack.io' }
	maven { url 'https://maven.terraformersmc.com/releases' }
	maven { url = "https://maven.isxander.dev/releases" }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered {
		it.addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${rootProject.minecraft_version}+build.3:v2"))
		// https://lambdaurora.dev/tools/import_quilt.html
		it.officialMojangMappings {
			setNameSyntheticMembers(false)
		}
	}

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Modmenu (for configuring the mod in-game)
	modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

	// Cloth Config
	modImplementation "dev.isxander:yet-another-config-lib:${project.yacl_version}"
}


processResources {
	inputs.property "version", rootProject.version

	filesMatching("fabric.mod.json") {
		expand "version": rootProject.version
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}
